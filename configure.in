# Configure template for GNU wdiff.
# Copyright (C) 1994, 1997, 1998, 1999 Free Software Foundation, Inc.
# Process this file with autoconf to produce a configure script.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software Foundation,
# Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.

AC_INIT(src/wdiff.c)
AC_PREREQ(2.12)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(wdiff, 0.5.4-pre1)
AT_CONFIG(../src)
ALL_LINGUAS="cs de es fr nl no pl ru sv"

test $fp_cv_prog_echo_nonl = no \
  && echo 2>&1 "WARNING: \`echo' not powerful enough for \`make check'"

AC_PATH_PROGS(DIFF, gnudiff diffgnu gdiff diff, diff)
case "$DIFF" in
  *gdiff)
    # Carefully avoid gdiff for X as found on SGI systems.
    (DISPLAY= gdiff /dev/null /dev/null) >/dev/null 2>&1 || DIFF=diff
    ;;
esac
dnl FIXME: Check that it is GNU diff indeed.

AC_PATH_PROGS(PAGER, less more)
AC_DEFINE_UNQUOTED(DIFF_PROGRAM, "$DIFF")
AC_DEFINE_UNQUOTED(PAGER_PROGRAM, "$PAGER")

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_AIX
AC_MINIX
AC_ISC_POSIX
AM_C_PROTOTYPES
AC_C_CONST
AC_C_INLINE

AC_CHECK_HEADERS(locale.h string.h unistd.h)
AC_HEADER_STDC
AC_TYPE_SIGNAL
AC_TYPE_SIZE_T

# Irix requires -lmalloc for some unknown reason.
AC_CHECK_LIB(malloc, malloc)

# Try discovering where tputs may be hiding.
AC_CHECK_LIB(termcap, tputs)
if test "$ac_cv_lib_termcap_tputs" = yes; then
  AC_DEFINE(HAVE_TPUTS)
  AC_CHECK_HEADERS(termcap.h)
else
  AC_CHECK_LIB(termlib, tputs)
  if test "$ac_cv_lib_termlib_tputs" = yes; then
    AC_DEFINE(HAVE_TPUTS)
    AC_CHECK_HEADERS(termlib.h)
  else
    AC_CHECK_LIB(curses, tputs)
    if test "$ac_cv_lib_curses_tputs" = yes; then
      AC_DEFINE(HAVE_TPUTS)
      AC_CHECK_HEADERS(curses.h)
    else
      AC_CHECK_LIB(ncurses, tputs)
      if test "$ac_cv_lib_ncurses_tputs" = yes; then
	AC_DEFINE(HAVE_TPUTS)
	AC_CHECK_HEADERS(ncurses.h)
      fi
    fi
  fi
fi

AC_CHECK_FUNCS(strerror)
AC_FUNC_ALLOCA
AC_FUNC_VPRINTF
AC_REPLACE_FUNCS(strstr)

dnl AC_MSG_CHECKING(if cbars is wanted)
dnl AC_ARG_ENABLE(cbars,
dnl [  --enable-cbars          enable installation of cbars],
dnl [if test "$enableval" = yes; then
dnl   AC_MSG_RESULT(yes); SCRIPTS="$SCRIPTS cbars"
dnl else
dnl   AC_MSG_RESULT(no)
dnl fi],
dnl [AC_MSG_RESULT(no)])

fp_WITH_GETTEXT
jm_FUNC_MALLOC
jm_FUNC_REALLOC
AM_WITH_DMALLOC

AC_OUTPUT([Makefile doc/Makefile i18n/Makefile lib/Makefile m4/Makefile
 src/Makefile src/cbars tests/Makefile tests/atconfig])
