#							-*- shell-script -*-

# wdiff test suite
# Copyright (C) 2010 Free Software Foundation, Inc.
#
# This file is part of GNU wdiff
#
# GNU wdiff is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# GNU wdiff is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Notes:
# - "@<:@" is the escape sequence for "["
# - "@:>@" is the escape sequence for "]"

AT_SETUP(wdiff output formats)
dnl      --------------------

AT_DATA(wdiff-a.txt,
[This is input1
The quick brown fox jumps over the lazy dog.
The hurried orange fox jumps over the lazy dog.
A slow green panda walks around a sleeping cat.
The middling red fox jumps over the lazy dog.
])

AT_DATA(wdiff-b.txt,
[This is input2
The quick brown fox jumps over the lazy dog.
The slow red fox jumps over the lazy dog.
A slow, short green giraffe walks around a sleeping cat.
The middling red fox jumps over the lazy dog.
])

AT_CHECK([wdiff wdiff-a.txt wdiff-b.txt], 1,
[This is @<:@-input1-@:>@ {+input2+}
The quick brown fox jumps over the lazy dog.
The @<:@-hurried orange-@:>@ {+slow red+} fox jumps over the lazy dog.
A @<:@-slow-@:>@ {+slow, short+} green @<:@-panda-@:>@ {+giraffe+} walks around a sleeping cat.
The middling red fox jumps over the lazy dog.
])

AT_CHECK([wdiff -1 wdiff-a.txt wdiff-b.txt], 1,
[This is {+input2+}
The quick brown fox jumps over the lazy dog.
The {+slow red+} fox jumps over the lazy dog.
A {+slow, short+} green {+giraffe+} walks around a sleeping cat.
The middling red fox jumps over the lazy dog.
])

AT_CHECK([wdiff -2 wdiff-a.txt wdiff-b.txt], 1,
[This is @<:@-input1-@:>@
The quick brown fox jumps over the lazy dog.
The @<:@-hurried orange-@:>@ fox jumps over the lazy dog.
A @<:@-slow-@:>@ green @<:@-panda-@:>@ walks around a sleeping cat.
The middling red fox jumps over the lazy dog.
])

AT_CHECK([wdiff -3 wdiff-a.txt wdiff-b.txt], 1,
[
======================================================================
 @<:@-input1-@:>@ {+input2+}
======================================================================
 @<:@-hurried orange-@:>@ {+slow red+}
======================================================================
 @<:@-slow-@:>@ {+slow, short+}
======================================================================
 @<:@-panda-@:>@ {+giraffe+}
======================================================================
])

AT_CHECK([wdiff -12 wdiff-a.txt wdiff-b.txt], 1,
[This is
======================================================================

The quick brown fox jumps over the lazy dog.
The
======================================================================
 fox jumps over the lazy dog.
A
======================================================================
 green
======================================================================
 walks around a sleeping cat.
The middling red fox jumps over the lazy dog.
])

AT_CHECK([wdiff -13 wdiff-a.txt wdiff-b.txt], 1,
[
======================================================================
 input2
======================================================================
 slow red
======================================================================
 slow, short
======================================================================
 giraffe
======================================================================
])

AT_CHECK([wdiff -23 wdiff-a.txt wdiff-b.txt], 1,
[
======================================================================
 input1
======================================================================
 hurried orange
======================================================================
 slow
======================================================================
 panda
======================================================================
])

AT_CHECK([wdiff -123 wdiff-a.txt wdiff-b.txt], 1)

AT_CHECK([wdiff -123s wdiff-a.txt wdiff-b.txt], 1,
[wdiff-a.txt: 39 words  34 87% common  0 0% deleted  5 12% changed
wdiff-b.txt: 40 words  34 85% common  0 0% inserted  6 15% changed
])

AT_CHECK([wdiff -w\( -x\) -y\<\< -z\>\> wdiff-a.txt wdiff-b.txt], 1,
[This is (input1) <<input2>>
The quick brown fox jumps over the lazy dog.
The (hurried orange) <<slow red>> fox jumps over the lazy dog.
A (slow) <<slow, short>> green (panda) <<giraffe>> walks around a sleeping cat.
The middling red fox jumps over the lazy dog.
])

AT_CLEANUP()
